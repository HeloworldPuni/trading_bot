//@version=6
indicator(title="VWAP", shorttitle="VWAP", overlay=true, timeframe="", timeframe_gaps=true)

// Exchange inputs
EXCHANGES_GROUP = "Exchange Selection"
useBinanceSpot = input.bool(true, "Binance Spot", group=EXCHANGES_GROUP)
useCoinbaseSpot = input.bool(true, "Coinbase Spot", group=EXCHANGES_GROUP)
useKrakenSpot = input.bool(true, "Kraken Spot", group=EXCHANGES_GROUP)
useBinancePerp = input.bool(true, "Binance Perp", group=EXCHANGES_GROUP)
useBybitPerp = input.bool(true, "Bybit Perp", group=EXCHANGES_GROUP)
useOkxPerp = input.bool(true, "OKX Perp", group=EXCHANGES_GROUP)

// Extract base symbol from current chart
rawTicker = syminfo.ticker
baseSymbol = str.replace(str.replace(str.replace(str.replace(str.replace(rawTicker, "USDT", ""), "USDC", ""), "USD", ""), "PERP", ""), ".P", "")

// Anchor settings
autoAnchor = timeframe.in_seconds() < 3600 ? "Week" : timeframe.in_seconds() <= 14400 ? "Month" : timeframe.in_seconds() <= 43200 ? "Quarter" : "Year"

var anchor = input.string(defval = "Auto", title="Anchor Period",
 options=["Auto", "Session", "Week", "Month", "Quarter", "Year", "Decade", "Century", "Earnings", "Dividends", "Splits"], group="VWAP Settings")

actualAnchor = anchor == "Auto" ? autoAnchor : anchor
src = input(title = "Source", defval = hlc3, group="VWAP Settings")

BANDS_GROUP = "Bands Settings"
calcModeInput = input.string("Standard Deviation", "Bands Calculation Mode", options = ["Standard Deviation", "Percentage"], group = BANDS_GROUP)
showBand_1 = input(true, title = "Show Bands", group = BANDS_GROUP, inline = "band_1")
bandMult = input.float(1.0, "Band Multiplier", group = BANDS_GROUP, step = 0.1)

PREV_PERIOD_GROUP = "Previous Period Settings"
showPrevPeriod = input.bool(true, title = "Show Previous VWAP", group = PREV_PERIOD_GROUP)

// Fetch data with conditional security calls
float bsPrice = na
float bsVol = 0.0
if useBinanceSpot
    bsPrice := request.security("BINANCE:" + baseSymbol + "USDT", timeframe.period, src, ignore_invalid_symbol=true)
    bsVol := request.security("BINANCE:" + baseSymbol + "USDT", timeframe.period, volume, ignore_invalid_symbol=true)

float cbPrice = na
float cbVol = 0.0
if useCoinbaseSpot
    cbPrice := request.security("COINBASE:" + baseSymbol + "USD", timeframe.period, src, ignore_invalid_symbol=true)
    cbVol := request.security("COINBASE:" + baseSymbol + "USD", timeframe.period, volume, ignore_invalid_symbol=true)

float krPrice = na
float krVol = 0.0
if useKrakenSpot
    krPrice := request.security("KRAKEN:" + baseSymbol + "USD", timeframe.period, src, ignore_invalid_symbol=true)
    krVol := request.security("KRAKEN:" + baseSymbol + "USD", timeframe.period, volume, ignore_invalid_symbol=true)

float bpPrice = na
float bpVol = 0.0
if useBinancePerp
    bpPrice := request.security("BINANCE:" + baseSymbol + "USDT.P", timeframe.period, src, ignore_invalid_symbol=true)
    bpVol := request.security("BINANCE:" + baseSymbol + "USDT.P", timeframe.period, volume, ignore_invalid_symbol=true)

float byPrice = na
float byVol = 0.0
if useBybitPerp
    byPrice := request.security("BYBIT:" + baseSymbol + "USDT.P", timeframe.period, src, ignore_invalid_symbol=true)
    byVol := request.security("BYBIT:" + baseSymbol + "USDT.P", timeframe.period, volume, ignore_invalid_symbol=true)

float okPrice = na
float okVol = 0.0
if useOkxPerp
    okPrice := request.security("OKX:" + baseSymbol + "USDT.P", timeframe.period, src, ignore_invalid_symbol=true)
    okVol := request.security("OKX:" + baseSymbol + "USDT.P", timeframe.period, volume, ignore_invalid_symbol=true)

// Aggregate
totalVol = nz(bsVol) + nz(cbVol) + nz(krVol) + nz(bpVol) + nz(byVol) + nz(okVol)
totalPV = nz(bsPrice * bsVol) + nz(cbPrice * cbVol) + nz(krPrice * krVol) + 
          nz(bpPrice * bpVol) + nz(byPrice * byVol) + nz(okPrice * okVol)

combinedPrice = totalVol > 0 ? totalPV / totalVol : src

// Period detection
new_earnings = request.earnings(syminfo.tickerid, earnings.actual, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_dividends = request.dividends(syminfo.tickerid, dividends.gross, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)
new_split = request.splits(syminfo.tickerid, splits.denominator, barmerge.gaps_on, barmerge.lookahead_on, ignore_invalid_symbol=true)

isNewPeriod = switch actualAnchor
	"Earnings"  => not na(new_earnings)
	"Dividends" => not na(new_dividends)
	"Splits"    => not na(new_split)
	"Session"   => timeframe.change("D")
	"Week"      => timeframe.change("W")
	"Month"     => timeframe.change("M")
	"Quarter"   => timeframe.change("3M")
	"Year"      => timeframe.change("12M")
	"Decade"    => timeframe.change("12M") and year % 10 == 0
	"Century"   => timeframe.change("12M") and year % 100 == 0
	=> false

isEsdAnchor = actualAnchor == "Earnings" or actualAnchor == "Dividends" or actualAnchor == "Splits"
if na(combinedPrice[1]) and not isEsdAnchor
	isNewPeriod := true

// VWAP calculation
float vwapValue = na
float upperBandValue1 = na
float lowerBandValue1 = na

[_vwap, _stdevUpper, _] = ta.vwap(combinedPrice, isNewPeriod, 1)
vwapValue := _vwap
stdevAbs = _stdevUpper - _vwap
bandBasis = calcModeInput == "Standard Deviation" ? stdevAbs * bandMult : _vwap * (0.01 * bandMult)
upperBandValue1 := _vwap + bandBasis
lowerBandValue1 := _vwap - bandBasis

// Store previous period
var float prevVwap = na
var float prevUpperBand = na
var float prevLowerBand = na

if isNewPeriod and not na(vwapValue[1])
    prevVwap := vwapValue[1]
    prevUpperBand := upperBandValue1[1]
    prevLowerBand := lowerBandValue1[1]

// Plots
plot(vwapValue, title = "VWAP", color = color.new(color.white, 25), linewidth = 2)
upperBand_1 = plot(upperBandValue1, title="Upper Band", color = color.new(color.white, 100), display = showBand_1 ? display.all : display.none)
lowerBand_1 = plot(lowerBandValue1, title="Lower Band", color = color.new(color.white, 100), display = showBand_1 ? display.all : display.none)
fill(upperBand_1, lowerBand_1, title="Bands Fill", color = color.new(color.white, 85), display = showBand_1 ? display.all : display.none)

prevVwapPlot = plot(showPrevPeriod ? prevVwap : na, title = "Previous VWAP", color = color.new(color.white, 25), linewidth = 2)
prevUpperPlot = plot(showPrevPeriod and showBand_1 ? prevUpperBand : na, title = "Previous Upper Band", color = color.new(color.white, 100), linewidth = 1)
prevLowerPlot = plot(showPrevPeriod and showBand_1 ? prevLowerBand : na, title = "Previous Lower Band", color = color.new(color.white, 100), linewidth = 1)
fill(prevUpperPlot, prevLowerPlot, title = "Previous Bands Fill", color = color.new(color.white, 85))