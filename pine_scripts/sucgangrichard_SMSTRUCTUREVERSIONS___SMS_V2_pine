//@version=5
indicator("Smart Market Structure", overlay=true, max_boxes_count=500, max_labels_count=500, max_lines_count=500)

// === Inputs === //
left        = input.int(10, "Swing Left", minval=1)
right       = input.int(10, "Swing Right", minval=1)
rayLength   = input.int(2, "Ray Length (for swing rays)", minval=1)
showSwings  = input.bool(true, "Show Swing High/Low Lines")
colorCandle = input.bool(true, "Color Candles by MSS")
showOB      = input.bool(true, "Show Order Blocks")
keepAllOBs  = input.bool(true, "Keep All OBs until Invalidation")
showDashboard = input.bool(true, "Show Dashboard")
dashboardPos = input.string("Top-Left", "Dashboard Position", options=["Top-Left", "Top-Right", "Bottom-Left", "Bottom-Right"])

// === Swing Points === //
swingHigh = ta.pivothigh(high, left, right)
swingLow  = ta.pivotlow(low, left, right)

var swingHighLines = array.new_line()
var swingLowLines  = array.new_line()

if showSwings
    if not na(swingHigh)
        l = line.new(x1=bar_index - right, y1=swingHigh,
                     x2=bar_index - right + rayLength, y2=swingHigh,
                     color=color.rgb(215, 199, 20), width=2, extend=extend.none)
        array.push(swingHighLines, l)

    if not na(swingLow)
        l = line.new(x1=bar_index - right, y1=swingLow,
                     x2=bar_index - right + rayLength, y2=swingLow,
                     color=color.rgb(215, 199, 20), width=2, extend=extend.none)
        array.push(swingLowLines, l)

// === Track Last Swings === //
var float lastSwingHigh = na
var float lastSwingLow  = na
var int   lastHighBar   = na
var int   lastLowBar    = na

if not na(swingHigh)
    lastSwingHigh := swingHigh
    lastHighBar   := bar_index - right

if not na(swingLow)
    lastSwingLow := swingLow
    lastLowBar   := bar_index - right

// === BOS & MSS === //
var int lastBOSHighBar = na
var int lastBOSLowBar  = na
var string lastBOSDir  = "none"

bosBull = not na(lastSwingHigh) and ta.crossover(close, lastSwingHigh) and (na(lastBOSHighBar) or lastHighBar != lastBOSHighBar)
bosBear = not na(lastSwingLow)  and ta.crossunder(close, lastSwingLow) and (na(lastBOSLowBar) or lastLowBar  != lastBOSLowBar)

mssBull = (lastBOSDir == "bear") and bosBull
mssBear = (lastBOSDir == "bull") and bosBear

centerX(_x1, _x2) =>
    int(math.round((_x1 + _x2) / 2.0))

var obBullRects = array.new_box()
var obBearRects = array.new_box()

createOB(isBull) =>
    if isBull
        box.new(bar_index - 1, high[1], bar_index, low[1],border_color=color.new(color.green, 0),bgcolor=color.new(color.green, 85), extend=extend.right)
    else
        box.new(bar_index - 1, high[1], bar_index, low[1],border_color=color.new(color.red, 0),bgcolor=color.new(color.red, 85), extend=extend.right)

if bosBull
    x1 = lastHighBar
    y  = lastSwingHigh
    caption = mssBull ? "MSS" : "BOS"
    _ = line.new(x1=x1, y1=y, x2=bar_index, y2=y,
                 color=color.new(color.green, 0), width=1,
                 style=line.style_dashed, extend=extend.none)
    label.new(centerX(x1, bar_index), y, caption,
              style=label.style_label_down, textcolor=color.white,
              color=color.new(#00ff08, 76), size=size.small)
    lastBOSHighBar := lastHighBar
    lastBOSDir := "bull"

    if showOB and close > lastSwingHigh
        ob = createOB(true)
        array.push(obBullRects, ob)
        if not keepAllOBs and array.size(obBullRects) > 1
            old = array.shift(obBullRects)
            box.delete(old)

if bosBear
    x1 = lastLowBar
    y  = lastSwingLow
    caption = mssBear ? "MSS" : "BOS"
    _ = line.new(x1=x1, y1=y, x2=bar_index, y2=y,
                 color=color.new(color.red, 0), width=1,
                 style=line.style_dashed, extend=extend.none)
    label.new(centerX(x1, bar_index), y, caption,
              style=label.style_label_up, textcolor=color.white,
              color=color.new(#ff0000, 81), size=size.small)
    lastBOSLowBar := lastLowBar
    lastBOSDir := "bear"

    if showOB and close < lastSwingLow
        ob = createOB(false)
        array.push(obBearRects, ob)
        if not keepAllOBs and array.size(obBearRects) > 1
            old = array.shift(obBearRects)
            box.delete(old)

invalidateOBs(rectArray, isBull) =>
    sz = array.size(rectArray)
    if sz > 0
        i = sz - 1
        while i >= 0
            r = array.get(rectArray, i)
            top = box.get_top(r)
            bot = box.get_bottom(r)
            if not na(top) and not na(bot)
                if isBull and close < bot
                    box.delete(r)
                    array.remove(rectArray, i)
                else if not isBull and close > top
                    box.delete(r)
                    array.remove(rectArray, i)
            i := i - 1

if showOB
    invalidateOBs(obBullRects, true)
    invalidateOBs(obBearRects, false)

var string structureTrend = "none"
if mssBull
    structureTrend := "bull"
if mssBear
    structureTrend := "bear"

// === Candle Coloring === //
var color candleColor = na
if colorCandle
    if structureTrend == "bull"
        candleColor := color.new(color.green, 0)
    else if structureTrend == "bear"
        candleColor := color.new(color.red, 0)
    else
        candleColor := na
else
    candleColor := na
barcolor(candleColor)

// === Dashboard === //
var table dash = na

if showDashboard
    if na(dash)
        pos = switch dashboardPos
            "Top-Left"     => position.top_left
            "Top-Right"    => position.top_right
            "Bottom-Left"  => position.bottom_left
            "Bottom-Right" => position.bottom_right
        dash := table.new(pos, 2, 6, frame_color=color.new(color.gray, 90), frame_width=1, border_width=1)

    // values & colors
    bosText  = lastBOSDir == "bull" ? "Bullish" : lastBOSDir == "bear" ? "Bearish" : "None"
    bosColor = lastBOSDir == "bull" ? color.green : lastBOSDir == "bear" ? color.red : color.gray

    mssText  = (structureTrend == "bull" and mssBull) ? "Bullish" : (structureTrend == "bear" and mssBear) ? "Bearish" : "None"
    mssColor = (structureTrend == "bull" and mssBull) ? color.green : (structureTrend == "bear" and mssBear) ? color.red : color.gray

    trendText  = structureTrend == "bull" ? "Bullish" : structureTrend == "bear" ? "Bearish" : "Neutral"
    trendColor = structureTrend == "bull" ? color.green : structureTrend == "bear" ? color.red : color.gray

    swingsSinceMSS = na(lastBOSHighBar) and na(lastBOSLowBar) ? 0 : bar_index - (structureTrend == "bull" ? lastBOSHighBar : lastBOSLowBar)
    swingsText = str.tostring(swingsSinceMSS)
    swingsColor = color.gray

    bullOBcount = array.size(obBullRects)
    bearOBcount = array.size(obBearRects)
    obText  = "Bull: " + str.tostring(bullOBcount) + " / Bear: " + str.tostring(bearOBcount)
    obColor = bullOBcount > bearOBcount ? color.green : bearOBcount > bullOBcount ? color.red : color.gray

    strengthPeriod = 8
    var float bullSwings = 0
    var float bearSwings = 0

    trendNum = structureTrend == "bull" ? 1 : structureTrend == "bear" ? -1 : 0

    if ta.change(trendNum)
        if trendNum == 1
            bullSwings += 1
        else if trendNum == 1
            bearSwings += 1
 
        if bullSwings + bearSwings > strengthPeriod
            bullSwings := bullSwings * 0.8
            bearSwings := bearSwings * 0.8

    strengthRatio = bullSwings / (bullSwings + bearSwings + 0.0001)
    strengthText  = strengthRatio > 0.65 ? "Strong Bull" : strengthRatio < 0.35 ? "Strong Bear" : "Neutral"
    strengthColor = strengthRatio > 0.65 ? color.green : strengthRatio < 0.35 ? color.red : color.gray

    table.cell(dash, 0, 0, "Last BOS",  text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 0, bosText,     text_color=color.white, bgcolor=bosColor)

    table.cell(dash, 0, 1, "Last MSS",  text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 1, mssText,     text_color=color.white, bgcolor=mssColor)

    table.cell(dash, 0, 2, "Trend",     text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 2, trendText,   text_color=color.white, bgcolor=trendColor)

    table.cell(dash, 0, 3, "Swings since MSS", text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 3, swingsText,         text_color=color.white, bgcolor=swingsColor)

    table.cell(dash, 0, 4, "Active OBs", text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 4, obText,       text_color=color.white, bgcolor=obColor)

    table.cell(dash, 0, 5, "Trend Strength", text_color=color.white, bgcolor=color.new(color.black, 0))
    table.cell(dash, 1, 5, strengthText,     text_color=color.white, bgcolor=strengthColor)