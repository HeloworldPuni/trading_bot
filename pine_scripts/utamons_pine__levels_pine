//@version=5
indicator("Auto Levels", shorttitle="Auto Levels", overlay=true, max_bars_back=960)

checkRange = input.int(80, title="Check Range", minval=0, tooltip = "A number of bars/candles from current point to the left to check for levels.")
luft = input.float(0.01, title="Luft", minval=0, step=0.01, tooltip = "Allowed deviation between vertices in the level.")
touches = input.int(3, title="Touches", minval=2, tooltip = "A number of touches required to detect the level (with luft)")
exactTouches = input.int(1, title="Exact Touches", minval=0, tooltip = "A number of exact (without the luft) touches required to detect the level")
numberOfLevels = input.int(2, title="Number of Levels", minval=1, tooltip = "A number of levels to keep in the chart")
lineLength = input.int(80, title="Line length", minval=40, tooltip = "The level line length")

var float[] highs = array.new_float()
var float[] lows = array.new_float()
var line[] lines = array.new_line()
var label[] labels = array.new_label()

// Function to check if a bar is a local high vertex
isHighVertex(index) =>
    high[index] >= high[index - 1] and high[index] >= high[index + 1]

// Function to check if a bar is a local low vertex
isLowVertex(index) =>
    low[index] <= low[index - 1] and low[index] <= low[index + 1]

shouldDrawLine(priceLevel) =>
    countLuft = 0
    countExact = 0
    if (array.size(highs) > 0)
        for i = 0 to array.size(highs) - 1
            if math.abs(array.get(highs, i) - priceLevel) <= luft
                countLuft := countLuft + 1
            if array.get(highs, i) == priceLevel
                countExact := countExact + 1
    if (array.size(lows) > 0)            
        for i = 0 to array.size(lows) - 1
            if math.abs(array.get(lows, i) - priceLevel) <= luft
                countLuft := countLuft + 1
            if array.get(lows, i) == priceLevel
                countExact := countExact + 1
    (luft > 0 and countExact >= exactTouches and countLuft >= touches) or (luft == 0 and countLuft >= touches)

countHighTouches(priceLevel) =>
    count = 0
    if (array.size(highs) > 0)
        for i = 0 to array.size(highs) - 1
            if math.abs(array.get(highs, i) - priceLevel) <= luft
                count := count + 1
    count

countLowTouches(priceLevel) =>
    count = 0
    if (array.size(lows) > 0)
        for i = 0 to array.size(lows) - 1
            if math.abs(array.get(lows, i) - priceLevel) <= luft
                count := count + 1
    count

drawLine(priceLevel, lineColor, style) =>
    // Remove the oldest line if we exceed the number of levels
    if array.size(lines) == numberOfLevels
        line.delete(array.shift(lines))
    l = line.new(bar_index - lineLength, priceLevel, bar_index, priceLevel, color=lineColor, style = style, width=math.floor(luft*100+1))
    array.push(lines, l)

mostTouched(countMore, countLess, isLimit) =>
    countMore > 0 and (countLess == 0 or countLess/countMore <= 0.8) and isLimit

// Add highs and lows only if they are vertices
if (isHighVertex(1))
    array.push(highs, high[1])
if (isLowVertex(1))
    array.push(lows, low[1])

if (array.size(highs) > checkRange)
    array.shift(highs)
if (array.size(lows) > checkRange)
    array.shift(lows)

// Check for matching highs and lows and draw a line
if (array.size(highs) > 0)
    colorHigh = color.blue
    colorLow = colorHigh
    style = line.style_dashed
    drawHigh = shouldDrawLine(high[1])
    drawLow = shouldDrawLine(low[1])
    HH = countHighTouches(high[1])
    LH = countLowTouches(high[1])
    HL = countHighTouches(low[1])
    LL = countLowTouches(low[1])
    if (mostTouched(HH, LH, drawHigh))
        colorHigh := color.red
        style := line.style_solid
    if (mostTouched(LH, HH, drawHigh))
        colorHigh := color.green
        style := line.style_solid
    if (mostTouched(LL, HL, drawLow))
        colorLow := color.green
        style := line.style_solid
    if (mostTouched(HL, LL, drawLow))
        colorLow := color.red
        style := line.style_solid
     
    if drawHigh
        drawLine(high[1], colorHigh, style)
    if drawLow
        drawLine(low[1], colorLow, style)
