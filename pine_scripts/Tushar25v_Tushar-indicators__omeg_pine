// Â© Tushar
// ADS indicator that plots swing-based levels, market structure states,
// and breakout statistics to help traders assess trend conditions and potential entries.

//@version=6
indicator('ADS [Tushar]', overlay = true, max_lines_count = 500)

// Functions

backtest(ms, x) =>
    // Track how often the current market structure direction was correct after x bars.
    var bt = array.new_int()
    if ms[x] > 0
        if close >= close[x]
            bt.push(1)
        else
            bt.push(0)
    if ms[x] < 0
        if close <= close[x]
            bt.push(1)
        else
            bt.push(0)
    if bt.size() > 2000
        bt.remove(0)
    wr = (bt.sum() / bt.size()) * 100 - 50

// Inputs

// Select how market structure is evaluated and stylize the plotted levels.
mode = input.string('Standard', 'Mode', ['Standard', 'Confirmed Breakout', 'Donchian Trend HL', 'Donchian Trend CC', 'Average'])
lnt = input.int(5, 'Pivot Length')
upc = input.color(#2962ff, '', inline = 'colors')
dnc = input.color(#e91e63, '', inline = 'colors')
lines = input.string('Dotted', 'Line Style', ['None', 'Solid', 'Dashed', 'Dotted'])

// Calculations

// Z-score and volume confirmation help filter exhaustion moves with strong activity.
zscore = (close - ta.sma(close, 21)) / ta.stdev(close, 21)
volconf = volume > math.avg(ta.sma(volume, 34), ta.ema(volume, 11))

// Detect swing highs/lows and Donchian style envelopes to evaluate structure shifts.
pvh = not na(ta.pivothigh(high, lnt, lnt))
pvl = not na(ta.pivotlow(low, lnt, lnt))
hh1 = ta.highest(high, lnt * 2)
hh2 = ta.highest(close, lnt * 2)
ll1 = ta.lowest(low, lnt * 2)
ll2 = ta.lowest(close, lnt * 2)
mid1 = math.avg(hh1, ll1)
mid2 = math.avg(hh2, ll2)

var ms3 = 0
if mid1 > mid1[1]
    ms3 := 1
if mid1 < mid1[1]
    ms3 := -1
var ms4 = 0
if mid2 > mid2[1]
    ms4 := 1
if mid2 < mid2[1]
    ms4 := -1

var phh = 0.00
var phc = 0.00
var pll = 0.00
var plc = 0.00

// Record the most recent confirmed swing points.
if pvh
    phh := high[lnt]
    phc := close[lnt]
if pvl
    pll := low[lnt]
    plc := close[lnt]

var pivothigh = 0.00
var pivotlow = 0.00
var ms1 = 0
// Market structure flip when price takes out pivot extremes.
if close > phh
    pivotlow := pll
    ms1 := 1
if close < pll
    pivothigh := phh
    ms1 := -1

var ms2 = 0
if close > pivothigh
    ms2 := 1
if close < pivotlow
    ms2 := -1

var line lineup = na
var line linend = na
var ph = 0.00
var pl = 0.00
var continuehigh = true
var continuelow = true

// Blend multiple structure variants when "Average" is selected.
ms5 = math.avg(ms1, ms2, ms3, ms4)
ms = mode == 'Standard' ? ms1 : mode == 'Confirmed Breakout' ? ms2 : mode == 'Donchian Trend HL' ? ms3 : mode == 'Donchian Trend CC' ? ms4 : ms5

bt1 = backtest(ms1, lnt)
bt2 = backtest(ms2, lnt)
bt3 = backtest(ms3, lnt)
bt4 = backtest(ms4, lnt)
bt5 = backtest(ms5, lnt)

// Aggregate backtest win rates across the different structure modes.
avgbt = math.avg(bt1, bt2, bt3, bt4)

// Color candles and signals based on current structure bias.
barc = ms > 0 ? upc : ms < 0 ? dnc : color.gray

long = zscore < -2 and ms > 0 and volconf
short = zscore > 2 and ms < 0 and volconf

// Convert textual input choice into line styles for plotted levels.
linestyle = switch lines
    'Solid' => line.style_solid
    'Dashed' => line.style_dashed
    'Dotted' => line.style_dotted
    'None' => na

// Plotting

if lines != 'None'
    // Maintain continuous resistance lines derived from swing highs.
    if pvh
        ph := high[lnt]
        lineup := line.new(x1 = bar_index - lnt, x2 = bar_index, y1 = ph, y2 = ph, color = dnc, style = linestyle)
        if continuehigh == true
            line.set_x2(lineup[1], bar_index - lnt)
        continuehigh := true
    else
        if high > ph
            continuehigh := false
    if continuehigh
        lineup.set_x2(bar_index)
    if continuehigh == false
        line.set_x2(lineup, bar_index)
    if high > lineup.get_price(bar_index - 1)
        lineup := line.new(x1 = bar_index, x2 = bar_index, y1 = high, y2 = high, color = dnc, style = linestyle)
        continuehigh := true

    // Maintain continuous support lines derived from swing lows.
    if pvl
        pl := low[lnt]
        linend := line.new(x1 = bar_index - lnt, x2 = bar_index, y1 = pl, y2 = pl, color = upc, style = linestyle)
        if continuelow == true
            line.set_x2(linend[1], bar_index - lnt)
        continuelow := true
    else
        if low < pl
            continuelow := false
    if continuelow
        linend.set_x2(bar_index)
    if continuelow == false
        line.set_x2(linend, bar_index)
    if low < linend.get_price(bar_index - 1)
        linend := line.new(x1 = bar_index, x2 = bar_index, y1 = low, y2 = low, color = upc, style = linestyle)
        continuelow := true

barcolor(barc)
plotcandle(open, high, low, close, 'Price', barc, barc, bordercolor = barc)

// Highlight overextended trades aligned with the structure bias.
plotshape(long, 'Long', shape.diamond, location.belowbar, upc, size = size.tiny)
plotshape(short, 'Short', shape.diamond, location.abovebar, dnc, size = size.tiny)

// Display a stats table showing win-rate by structure type.
tab = table.new(position.middle_right, 10, 10)
tab.cell(0, 1, 'Standard', text_color = chart.fg_color)
tab.cell(0, 2, 'ConfBreak', text_color = chart.fg_color)
tab.cell(0, 3, 'Donchian HL', text_color = chart.fg_color)
tab.cell(0, 4, 'Donchian CC', text_color = chart.fg_color)
tab.cell(0, 5, 'Average', text_color = chart.fg_color)
tab.cell(1, 0, 'Alpha', text_color = chart.fg_color)
tab.cell(1, 1, str.tostring(bt1, '#.##') + '%', text_color = chart.fg_color)
tab.cell(1, 2, str.tostring(bt2, '#.##') + '%', text_color = chart.fg_color)
tab.cell(1, 3, str.tostring(bt3, '#.##') + '%', text_color = chart.fg_color)
tab.cell(1, 4, str.tostring(bt4, '#.##') + '%', text_color = chart.fg_color)
tab.cell(1, 5, str.tostring(bt5, '#.##') + '%', text_color = chart.fg_color)

// End of Script